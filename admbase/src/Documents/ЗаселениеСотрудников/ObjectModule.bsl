
////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//  
////////////////////////////////////////////////////////////////////////////////
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

//@skip-check module-region-empty
#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытий


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ПроверкаЗаселенных(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
#Область ТекстЗапроса

	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаселениеСотрудников.Дата КАК Период,
	|	ЗаселениеСотрудников.Дата КАК ДатаЗаписи,
	|	ЗаселениеСотрудников.Ссылка КАК Регистратор,
	|	ЗаселениеСотрудников.АдресРегистрации,
	|	ЗаселениеСотрудников.ДатаЗаселения,
	|	ЗаселениеСотрудниковДетальныеЗаписи.Сотрудник,
	|	ЗаселениеСотрудниковДетальныеЗаписи.Комната,
	|	ЗаселениеСотрудниковДетальныеЗаписи.Место,
	|	ЗаселениеСотрудников.СтоимостьПроживания
	|ИЗ
	|	Документ.ЗаселениеСотрудников.ДетальныеЗаписи КАК ЗаселениеСотрудниковДетальныеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаселениеСотрудников КАК ЗаселениеСотрудников
	|		ПО ЗаселениеСотрудниковДетальныеЗаписи.Ссылка = ЗаселениеСотрудников.Ссылка
	|ГДЕ
	|	ЗаселениеСотрудниковДетальныеЗаписи.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаселениеСотрудниковДетальныеЗаписи.НомерСтроки";

#КонецОбласти

					   
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Движения.ИсторияРазмещенияСотрудников.Добавить(),Выборка);
		ЗаполнитьЗначенияСвойств(Движения.ЗанятыеМестаОбщежитие.Добавить(),Выборка);
	КонецЦикла;
	
	Движения.ИсторияРазмещенияСотрудников.Записывать = Истина;
	Движения.ЗанятыеМестаОбщежитие.Записывать = Истина;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДетальныеЗаписи.Очистить();	

КонецПроцедуры


#КонецОбласти

//@skip-check module-region-empty
#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаЗаселенных(Отказ)

Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДетальныеЗаписи", ДетальныеЗаписи.Выгрузить());

#Область ТекстЗапроса

	Запрос.Текст = "ВЫБРАТЬ
				   |	ДетальныеЗаписи.Сотрудник,
				   |	ДетальныеЗаписи.Комната,
				   |	ДетальныеЗаписи.Место,
				   |	ДетальныеЗаписи.НомерСтроки
				   |ПОМЕСТИТЬ ДетальныеЗаписи
				   |ИЗ
				   |	&ДетальныеЗаписи КАК ДетальныеЗаписи
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	Представление(ЗанятыеМестаОбщежитие.АдресРегистрации) КАК АдресРегистрации,
				   |	Представление(ЗанятыеМестаОбщежитие.Комната) КАК Комната,
				   |	Представление(ЗанятыеМестаОбщежитие.Место) КАК Место,
				   |	Представление(ЗанятыеМестаОбщежитие.Сотрудник) КАК Сотрудник,
				   |	ЗанятыеМестаОбщежитие.ДатаЗаписи,
				   |	ЗанятыеМестаОбщежитие.ДатаЗаселения,
				   |	ДетальныеЗаписи.НомерСтроки КАК НомерСтроки
				   |ИЗ
				   |	РегистрСведений.ЗанятыеМестаОбщежитие КАК ЗанятыеМестаОбщежитие
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДетальныеЗаписи КАК ДетальныеЗаписи
				   |		ПО ЗанятыеМестаОбщежитие.Сотрудник = ДетальныеЗаписи.Сотрудник
				   |
				   |УПОРЯДОЧИТЬ ПО
				   |	НомерСтроки";

#КонецОбласти

	Результат =Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;

	Массив = Новый Массив;
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл

		Массив.Добавить(СтрШаблон("стр.%1 %2 уже размещен %3 место %4 по адресу %5", Выборка.НомерСтроки, 
														Выборка.Сотрудник, Выборка.Комната,
														Выборка.Место, Выборка.АдресРегистрации));

	КонецЦикла;

	ОбщегоНазначения.СообщитьПользователю(СтрСоединить(Массив, Символы.ПС), , , , Отказ);

КонецПроцедуры

#КонецОбласти 

#КонецЕсли


