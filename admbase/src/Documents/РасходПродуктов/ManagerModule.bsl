#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции


// 
// Параметры:
//  ДокументОбъект 	- ДокументОбъект
//  Номенклатура 	- СправочникСсылка.Номенклатура
//  Реквизиты - Строка - Реквизиты
Процедура ЗаполнениеПриИзмененииМеню(ДокументОбъект,Номенклатура,Реквизиты="КатегорияМеню,ВидСтола") Экспорт 
	
	Если Не ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Истина)
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
	Запрос.Текст = "ВЫБРАТЬ
	|	Номенклатура.КатегорияМеню КАК КатегорияМеню,
	|	Номенклатура.ВидСтола КАК ВидСтола
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект,Выборка,Реквизиты);

КонецПроцедуры

// Заполнение табличной части состав стола.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект
Процедура ЗаполнениеТабличнойЧастиСоставСтола(ДокументОбъект)  Экспорт

	Если Не ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Истина)
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",				ДокументОбъект.Номенклатура);
	Запрос.УстановитьПараметр("КоличествоПорций",	ДокументОбъект.Количество);
	
#Область ТекстЗапроса

	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НоменклатураСоставНабора.Номенклатура КАК Номенклатура,
	|	НоменклатураСоставНабора.Количество * &КоличествоПорций КАК Количество,
	|	НоменклатураСоставНабора.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЕдиницыИзмерения.Вес * НоменклатураСоставНабора.Количество * &КоличествоПорций КАК Вес,
	|	ЕдиницыИзмерения.Вес КАК ВесЕдиницы
	|ИЗ
	|	Справочник.Номенклатура.СоставНабора КАК НоменклатураСоставНабора
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО НоменклатураСоставНабора.Ссылка = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО НоменклатураСоставНабора.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|ГДЕ
	|	НоменклатураСоставНабора.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатураСоставНабора.НомерСтроки";
	
#КонецОбласти

	ДокументОбъект.СоставСтола.Очистить();
	ДокументОбъект.Продукты.Очистить();
	
	Выборка = Запрос.Выполнить().Выбрать();
	КодСтроки = 1;
	Пока  Выборка.Следующий() Цикл
		
		СтрокаСтол = ДокументОбъект.СоставСтола.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтол,Выборка);
		СтрокаСтол.КодСтроки = КодСтроки;
		
		КодСтроки = КодСтроки + 1;
		
	КонецЦикла;


КонецПроцедуры

Процедура ЗаполнениеТабличнойчастиПродукты(ДокументОбъект) Экспорт 

	Если  Не ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Истина)
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СоставСтола",ДокументОбъект.СоставСтола.Выгрузить());
	//1. Получаем "Состав стола"
	//2. Выбираем только "Блюда"
	//3. Из справочника номенклатура получаем сотав блюд - это будут продукты
	//4. Из Состава стола выделяем продукты
	//5. Получаем данные справочника номенклатура для продуктов
	//6. Выполняем пересчет из единиц рецептов в единицы хранения. при установленном флаге округляем до целого
	
#Область ТекстЗапроса

Запрос.Текст = "ВЫБРАТЬ
|	РасходПродуктовСоставСтола.Номенклатура КАК Номенклатура,
|	РасходПродуктовСоставСтола.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
|	РасходПродуктовСоставСтола.Количество КАК Количество
|ПОМЕСТИТЬ СоставСтола
|ИЗ
|	&СоставСтола КАК РасходПродуктовСоставСтола
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	СоставСтола.Номенклатура КАК Номенклатура,
|	СоставСтола.Количество КАК Количество,
|	СправочникНоменклатура.КоличествоБлюд КАК ЗнаменательРецепта
|ПОМЕСТИТЬ ШапкаБлюда
|ИЗ
|	СоставСтола КАК СоставСтола
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
|		ПО СоставСтола.Номенклатура = СправочникНоменклатура.Ссылка
|			И (СправочникНоменклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.Блюда))
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	НоменклатураСоставНабора.Номенклатура КАК Номенклатура,
|	НоменклатураСоставНабора.Количество * ШапкаБлюда.Количество / ШапкаБлюда.ЗнаменательРецепта КАК Количество,
|	НоменклатураСоставНабора.ЕдиницаИзмерения КАК ЕдиницаИзмерения
|ПОМЕСТИТЬ ПродуктыПредварительно
|ИЗ
|	Справочник.Номенклатура.СоставНабора КАК НоменклатураСоставНабора
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ШапкаБлюда КАК ШапкаБлюда
|		ПО НоменклатураСоставНабора.Ссылка = ШапкаБлюда.Номенклатура
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	СоставСтола.Номенклатура,
|	СоставСтола.Количество,
|	СоставСтола.ЕдиницаИзмерения
|ИЗ
|	СоставСтола КАК СоставСтола
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
|		ПО СоставСтола.Номенклатура = СправочникНоменклатура.Ссылка
|			И (СправочникНоменклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.Продукты))
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ПродуктыПредварительно.Номенклатура КАК Номенклатура,
|	СУММА(ПродуктыПредварительно.Количество) КАК Количество,
|	ПродуктыПредварительно.ЕдиницаИзмерения КАК ЕдиницаИзмерения
|ПОМЕСТИТЬ ГруппировкаПродукты
|ИЗ
|	ПродуктыПредварительно КАК ПродуктыПредварительно
|
|СГРУППИРОВАТЬ ПО
|	ПродуктыПредварительно.Номенклатура,
|	ПродуктыПредварительно.ЕдиницаИзмерения
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	СправчникНоменклатура.Ссылка КАК Ссылка,
|	СправчникНоменклатура.ЕдиницаДляРецептов КАК ЕдиницаДляРецептов,
|	СправчникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
|	СправчникНоменклатура.СодержитЕдиницХранения КАК СодержитЕдиницХранения,
|	СправчникНоменклатура.ЕдиницаИзмерения.ОкруглятьДоЦелого КАК ОкруглятьДоЦелого,
|	ЕСТЬNULL(СправчникНоменклатура.ЕдиницаИзмерения.Вес, 0) КАК ЕдиницаИзмеренияВес,
|	СправчникНоменклатура.Наименование КАК Наименование
|ПОМЕСТИТЬ ДанныеНоменклатуры
|ИЗ
|	Справочник.Номенклатура КАК СправчникНоменклатура
|ГДЕ
|	СправчникНоменклатура.Ссылка В
|			(ВЫБРАТЬ
|				ГруппировкаПродукты.Номенклатура КАК Номенклатура
|			ИЗ
|				ГруппировкаПродукты КАК ГруппировкаПродукты)
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ГруппировкаПродукты.Номенклатура КАК Номенклатура,
|	ВЫБОР
|		КОГДА ГруппировкаПродукты.ЕдиницаИзмерения = ДанныеНоменклатуры.ЕдиницаИзмерения
|			ТОГДА ГруппировкаПродукты.Количество
|		ИНАЧЕ ВЫБОР
|				КОГДА ДанныеНоменклатуры.ОкруглятьДоЦелого
|					ТОГДА ВЫРАЗИТЬ(ГруппировкаПродукты.Количество / ДанныеНоменклатуры.СодержитЕдиницХранения КАК ЧИСЛО(15, 0))
|				ИНАЧЕ ГруппировкаПродукты.Количество / ДанныеНоменклатуры.СодержитЕдиницХранения
|			КОНЕЦ
|	КОНЕЦ КАК Количество,
|	ДанныеНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
|	ВЫБОР
|		КОГДА ГруппировкаПродукты.ЕдиницаИзмерения = ДанныеНоменклатуры.ЕдиницаИзмерения
|			ТОГДА ГруппировкаПродукты.Количество
|		ИНАЧЕ ВЫБОР
|				КОГДА ДанныеНоменклатуры.ОкруглятьДоЦелого
|					ТОГДА ВЫРАЗИТЬ(ГруппировкаПродукты.Количество / ДанныеНоменклатуры.СодержитЕдиницХранения КАК ЧИСЛО(15, 0))
|				ИНАЧЕ ГруппировкаПродукты.Количество / ДанныеНоменклатуры.СодержитЕдиницХранения
|			КОНЕЦ
|	КОНЕЦ * ДанныеНоменклатуры.ЕдиницаИзмеренияВес КАК Вес
|ИЗ
|	ГруппировкаПродукты КАК ГруппировкаПродукты
|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеНоменклатуры КАК ДанныеНоменклатуры
|		ПО ГруппировкаПродукты.Номенклатура = ДанныеНоменклатуры.Ссылка
|
|УПОРЯДОЧИТЬ ПО
|	ДанныеНоменклатуры.Наименование"; 



#КонецОбласти
	
	ДокументОбъект.Продукты.Загрузить(Запрос.Выполнить().Выгрузить());


КонецПроцедуры

#КонецОбласти 

#КонецЕсли 