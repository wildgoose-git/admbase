#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Макет = Документы.СтоловаяPerco.ПолучитьМакет("Макет_MXL_Загрузка");
	ТабличныйДокумент.Вывести(Макет);
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьСЗагрузкой(Команда)
	
	МассивСтруктур = ПолучитьМассивСтруктур();
	Если Не МассивСтруктур = Неопределено Тогда
		Закрыть(МассивСтруктур);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьМассивСтруктур()

	МассивСтруктур = Новый Массив;
	ВысотаТаблицы = ТабличныйДокумент.ВысотаТаблицы;
	Для  Счетчик = 1 По ВысотаТаблицы Цикл
		
		НомерСтроки = СтрЗаменить(Строка(Счетчик),Символы.НПП,"");
		
		Если ПустаяСтрока(ТабличныйДокумент.Область(СтрШаблон("R%1C1",НомерСтроки)).Текст) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураСтроки = ИнициализироватьСтруктуру();
		СтруктураСтроки.Сотрудник 				= ТабличныйДокумент.Область(СтрШаблон("R%1C1",НомерСтроки)).Текст;
		СтруктураСтроки.Помещение 				= ТабличныйДокумент.Область(СтрШаблон("R%1C4",НомерСтроки)).Текст;
		
		НетОшибок = Истина;
		Попытка
			СтруктураСтроки.Дата 					= ЗначениеДатыИзСтроки(ТабличныйДокумент.Область(СтрШаблон("R%1C2",НомерСтроки)).Текст);
		Исключение
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = СтрШаблон("стр %1 %2 %3",НомерСтроки,ТабличныйДокумент.Область(СтрШаблон("R%1C2",НомерСтроки)).Текст,ОписаниеОшибки());
			СообщениеПользователю.Сообщить();
			НетОшибок = Ложь;
		КонецПопытки;
		
		Попытка
			СтруктураСтроки.Время 					= ЗначениеВремяИзСтроки(ТабличныйДокумент.Область(СтрШаблон("R%1C3",НомерСтроки)).Текст);
		Исключение
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = СтрШаблон("стр %1 %2 %3",НомерСтроки,ТабличныйДокумент.Область(СтрШаблон("R%1C3",НомерСтроки)).Текст,ОписаниеОшибки());
			СообщениеПользователю.Сообщить(); 
			НетОшибок = Ложь;
		КонецПопытки;

		Если  НетОшибок Тогда 
			МассивСтруктур.Добавить(СтруктураСтроки);
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла; 	 

	Возврат МассивСтруктур;
	
КонецФункции // ПолучитьМассивСтруктур()

&НаКлиенте
Функция ИнициализироватьСтруктуру()
	
	СтруктураСтроки = Новый Структура;
	СтруктураСтроки.Вставить("Сотрудник");
	СтруктураСтроки.Вставить("Дата");
	СтруктураСтроки.Вставить("Время");
	СтруктураСтроки.Вставить("Помещение");
	Возврат СтруктураСтроки;
	
КонецФункции // ИнициализироватьСтруктуру()

&НаКлиенте
Функция ЗначениеДатыИзСтроки(Знач Текст)
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат '00010101'
	КонецЕсли;
	
	Число 	= Сред(Текст,1,2);
	Месяц 	= Сред(Текст,4,2);
	Год 	= Сред(Текст,7,4);
	
	Возврат  Дата(СтрШаблон("%1%2%3",Год,Месяц,Число));
	
КонецФункции // ЗначениеДатыИзСтроки()

&НаКлиенте
Функция ЗначениеВремяИзСтроки(Знач Текст)
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат '00010101'
	КонецЕсли;
		
	ПозицияДвоеточия = СтрНайти(Текст,":");
	
	Часы            = Сред(Текст,1,ПозицияДвоеточия-1);
	Минуты          = Сред(Текст,ПозицияДвоеточия+1,2);
	
	Пока СтрДлина(Часы) < 2 Цикл 
		Часы = СтрШаблон("0%1",Часы);
	КонецЦикла; 
	Пока СтрДлина(Минуты) < 2 Цикл 
		Минуты = СтрШаблон("0%1",Минуты);
	КонецЦикла;
	
	ДатаСтрокой = СтрЗаменить(СтрШаблон("00010101.%1.%2.00",Часы,Минуты),".","");

	Попытка
		Возврат Дата(ДатаСтрокой)
	Исключение
		Возврат '00010101';
	КонецПопытки;

	
КонецФункции // ЗначениеВремяИзСтроки()

#КонецОбласти 