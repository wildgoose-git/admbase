#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;

	ПроверкаКоррекностиВводаСотрудников(Отказ,РежимЗаписи);	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;

	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(НазначениеИндивидуальныхГрафиковИсходныеДанные.Дата, МЕСЯЦ) КАК Месяц
	|ИЗ
	|	Документ.НазначениеИндивидуальныхГрафиков.ИсходныеДанные КАК НазначениеИндивидуальныхГрафиковИсходныеДанные
	|ГДЕ
	|	НазначениеИндивидуальныхГрафиковИсходныеДанные.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(НазначениеИндивидуальныхГрафиковИсходныеДанные.Дата, МЕСЯЦ)";
	
	МассивДаты = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивДаты.Добавить(Выборка.Месяц)
	КонецЦикла;
	
	КонсолидацияРегистрЗаданий.ОтменитьРассчетВремениВПериоде(МассивДаты);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаИсходныеДанные.Сотрудник КАК Сотрудник,
	|	ТаблицаИсходныеДанные.Дата КАК Период,
	|	ТаблицаИсходныеДанные.РабочееВремяНачало КАК РабочееВремяНачало,
	|	ТаблицаИсходныеДанные.РабочееВремяОкончание КАК РабочееВремяОкончание,
	|	ТаблицаИсходныеДанные.ПерерывНачало КАК ПерерывНачало,
	|	ТаблицаИсходныеДанные.ПерерывОкончание КАК ПерерывОкончание,
	|	НазначениеИндивидуальныхГрафиков.СхемаРабочегоВремени КАК СхемаРабочегоВремени,
	|	НазначениеИндивидуальныхГрафиков.Календарь КАК Календарь
	|ИЗ
	|	Документ.НазначениеИндивидуальныхГрафиков.ИсходныеДанные КАК ТаблицаИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НазначениеИндивидуальныхГрафиков КАК НазначениеИндивидуальныхГрафиков
	|		ПО ТаблицаИсходныеДанные.Ссылка = НазначениеИндивидуальныхГрафиков.Ссылка
	|ГДЕ
	|	ТаблицаИсходныеДанные.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	Период";
	
	Движения.ГрафикиРаботИндивидуальные.Загрузить(Запрос.Выполнить().Выгрузить());
	Движения.ГрафикиРаботИндивидуальные.Записывать = Истина;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаКоррекностиВводаСотрудников(Отказ,РежимЗаписи)
	
	Если Не РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда 
		 Возврат
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсходныеДанные",ИсходныеДанные);
	Запрос.Текст = "ВЫБРАТЬ
	|	ИсходныеДанные.Сотрудник КАК Сотрудник,
	|	ИсходныеДанные.Дата КАК Дата,
	|	ИсходныеДанные.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	&ИсходныеДанные КАК ИсходныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанные.Сотрудник КАК Сотрудник,
	|	ИсходныеДанные.Дата КАК Дата,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИсходныеДанные.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ЗадвоенныеСтроки
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсходныеДанные.Сотрудник,
	|	ИсходныеДанные.Дата
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИсходныеДанные.НомерСтроки) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанные.Сотрудник КАК Сотрудник,
	|	ИсходныеДанные.Дата КАК Дата,
	|	ИсходныеДанные.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗадвоенныеСтроки КАК ЗадвоенныеСтроки
	|		ПО ИсходныеДанные.Сотрудник = ЗадвоенныеСтроки.Сотрудник
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	НомерСтроки";
	
	Результат = Запрос.Выполнить();
	Если  Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;

	Выборка = Результат.Выбрать();
	
	Массив = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Массив.Добавить(СтрШаблон("ном. строки %1; %2 %3;  %4",Строка(Выборка.НомерСтроки),
						Символы.Таб,
						Формат(Выборка.Дата,"ДЛФ=Д"),
						Строка(Выборка.Сотрудник)))
	КонецЦикла;
	
	ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Задвоенные данные у сотрудников%1%2",Символы.ПС,СтрСоединить(Массив,Символы.ПС)),,,,Отказ);

КонецПроцедуры
			 
#КонецОбласти 

#КонецЕсли