
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
#Область ТекстЗапроса
Запрос.Текст = "ВЫБРАТЬ
|	РасходПродуктовПродукты.Номенклатура КАК Номенклатура,
|	РасходПродуктовПродукты.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
|	РасходПродуктовПродукты.Количество КАК Количество,
|	РасходПродуктовПродукты.Вес КАК Вес,
|	РасходПродуктов.Дата КАК Период,
|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
|	РасходПродуктов.МестоХранения КАК МестоХранения
|ИЗ
|	Документ.РасходПродуктов.Продукты КАК РасходПродуктовПродукты
|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходПродуктов КАК РасходПродуктов
|		ПО РасходПродуктовПродукты.Ссылка = РасходПродуктов.Ссылка
|ГДЕ
|	РасходПродуктовПродукты.Ссылка = &Ссылка
|
|УПОРЯДОЧИТЬ ПО
|	РасходПродуктовПродукты.НомерСтроки
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	РасходПродуктовСоставСтола.Номенклатура КАК Номенклатура,
|	РасходПродуктовСоставСтола.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
|	РасходПродуктовСоставСтола.Количество КАК Количество,
|	РасходПродуктов.Дата КАК Период,
|	РасходПродуктов.ВидСтола КАК ВидСтола,
|	РасходПродуктов.КатегорияМеню КАК КатегорияМеню
|ИЗ
|	Документ.РасходПродуктов.СоставСтола КАК РасходПродуктовСоставСтола
|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходПродуктов КАК РасходПродуктов
|		ПО РасходПродуктовСоставСтола.Ссылка = РасходПродуктов.Ссылка
|ГДЕ
|	РасходПродуктовСоставСтола.Ссылка = &Ссылка
|	И РасходПродуктовСоставСтола.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.Блюда)
|
|УПОРЯДОЧИТЬ ПО
|	РасходПродуктовСоставСтола.НомерСтроки
|";
	
#КонецОбласти

	Результат = Запрос.ВыполнитьПакет();
	
	Движения.ОстаткиПродуктовНаСкладах.Записывать = Истина;
	
	Выборка = Результат[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Движения.ОстаткиПродуктовНаСкладах.Добавить(),Выборка)
	КонецЦикла;

	
	Движения.ИзготовлениеБлюд.Записывать = Истина;
	
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Движения.ИзготовлениеБлюд.Добавить(),Выборка)
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ИзКалендаря") Тогда
		 ЗаполнитьИзКалендаря(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьИзКалендаря(СтруктураКалендаря)

	//шапка
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,СтруктураКалендаря);
	
	Документы.РасходПродуктов.ЗаполнениеПриИзмененииМеню(ЭтотОбъект,Номенклатура,"КатегорияМеню");
	Ответственный = Пользователи.ТекущийПользователь();
	
	//тч составстола
	Документы.РасходПродуктов.ЗаполнениеТабличнойЧастиСоставСтола(ЭтотОбъект);

	//тч продукты
	Документы.РасходПродуктов.ЗаполнениеТабличнойчастиПродукты(ЭтотОбъект);

КонецПроцедуры



#КонецОбласти 

//СДЕЛАТЬ КОНТРОЛЬ ПРОВЕДЕНИЯ И ОТМЕНЫ ПРОВЕДЕНИЯ
#КонецЕсли