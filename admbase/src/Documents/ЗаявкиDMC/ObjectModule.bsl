
////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//  
////////////////////////////////////////////////////////////////////////////////
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

//1. Формируем общую таблицу заявок на каждую дату
//2. Вытесняем задвоенные заявки самой поздней заявкой
//3. Формируем таблицу движений

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",			Ссылка);
	Запрос.УстановитьПараметр("ПустаяДата",		'00010101');
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаявкиDMCИсходныеДанные.Сотрудник КАК Сотрудник,
	|	ЗаявкиDMCИсходныеДанные.ДатаНачала КАК Дата,
	|	ЗаявкиDMCИсходныеДанные.НомерСтроки КАК СодержаниеЗаявкиНомерСтроки,
	|	ЗаявкиDMCИсходныеДанные.РегистрационныйНомер КАК РегистрационныйНомер,
	|	ЗаявкиDMCИсходныеДанные.ДатаДокумента КАК ДатаДокумента,
	|	ЗаявкиDMCИсходныеДанные.ПричинаОтсутствия КАК ПричинаОтсутствия,
	|	ЗаявкиDMCИсходныеДанные.ВремяНачала КАК ВремяНачалаДатой,
	|	РАЗНОСТЬДАТ(&ПустаяДата, ЗаявкиDMCИсходныеДанные.ВремяНачала, СЕКУНДА) КАК ВремяНачалаЧислом,
	|	ЗаявкиDMCИсходныеДанные.ВремяОкончания КАК ВремяОкончанияДатой,
	|	РАЗНОСТЬДАТ(&ПустаяДата, ЗаявкиDMCИсходныеДанные.ВремяОкончания, СЕКУНДА) КАК ВремяОкончанияЧислом,
	|	ЗаявкиDMCИсходныеДанные.НомерСтроки КАК СтрокаДокумента,
	|	ЗаявкиDMCИсходныеДанные.Ссылка КАК ДокументДвижения,
	|	ВЫБОР
	|		КОГДА ЗаявкиDMCИсходныеДанные.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РАЗНОСТЬДАТ(ЗаявкиDMCИсходныеДанные.ДатаНачала, КОНЕЦПЕРИОДА(ЗаявкиDMCИсходныеДанные.ДатаНачала, МЕСЯЦ), ДЕНЬ)
	|		ИНАЧЕ РАЗНОСТЬДАТ(ЗаявкиDMCИсходныеДанные.ДатаНачала, ЗаявкиDMCИсходныеДанные.ДатаОкончания, ДЕНЬ)
	|	КОНЕЦ КАК РазностьДат,
	|	ЗаявкиDMCИсходныеДанные.ДатаОкончания КАК ДатаОкончания,
	|	ЗаявкиDMCИсходныеДанные.ДатаНачала КАК ДатаНачала
	|ИЗ
	|	Документ.ЗаявкиDMC.ИсходныеДанные КАК ЗаявкиDMCИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкиDMC КАК ЗаявкиDMC
	|		ПО ЗаявкиDMCИсходныеДанные.Ссылка = ЗаявкиDMC.Ссылка
	|ГДЕ
	|	ЗаявкиDMCИсходныеДанные.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	ТаблицаЗначений = Новый ТаблицаЗначений;
	Для каждого Колонка Из  Результат.Колонки Цикл
		ТаблицаЗначений.Колонки.Добавить(Колонка.Имя,Колонка.ТипЗначения);
	КонецЦикла;
	
	ЗаполнитьТаблицуДвиженийБезПрогулов(Результат.Выбрать(),ТаблицаЗначений);
	
	Запрос.УстановитьПараметр("ТаблицаЗначений",ТаблицаЗначений);
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаЗначений.Сотрудник КАК Сотрудник,
	|	ТаблицаЗначений.Дата КАК Дата,
	|	ТаблицаЗначений.СодержаниеЗаявкиНомерСтроки КАК СодержаниеЗаявкиНомерСтроки,
	|	ТаблицаЗначений.РегистрационныйНомер КАК НомерДокументаDMC,
	|	ТаблицаЗначений.ДатаДокумента КАК ДатаДокументаDMC,
	|	ТаблицаЗначений.ПричинаОтсутствия КАК ПричинаОтсутствия,
	|	ТаблицаЗначений.ВремяНачалаДатой КАК ВремяНачалаДатой,
	|	ТаблицаЗначений.ВремяНачалаЧислом КАК ВремяНачалаЧислом,
	|	ТаблицаЗначений.ВремяОкончанияДатой КАК ВремяОкончанияДатой,
	|	ТаблицаЗначений.ВремяОкончанияЧислом КАК ВремяОкончанияЧислом,
	|	ТаблицаЗначений.СтрокаДокумента КАК СтрокаДокумента,
	|	ТаблицаЗначений.ДокументДвижения КАК ДокументДвижения
	|ПОМЕСТИТЬ ТаблицаЗначений
	|ИЗ
	|	&ТаблицаЗначений КАК ТаблицаЗначений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗначений.Сотрудник КАК Сотрудник,
	|	ТаблицаЗначений.Дата КАК Дата,
	|	МАКСИМУМ(ТаблицаЗначений.НомерДокументаDMC) КАК НомерДокументаDMC
	|ПОМЕСТИТЬ Отсекающая
	|ИЗ
	|	ТаблицаЗначений КАК ТаблицаЗначений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗначений.Сотрудник,
	|	ТаблицаЗначений.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗначений.Сотрудник КАК Сотрудник,
	|	ТаблицаЗначений.Дата КАК Дата,
	|	ТаблицаЗначений.СодержаниеЗаявкиНомерСтроки КАК СодержаниеЗаявкиНомерСтроки,
	|	ТаблицаЗначений.НомерДокументаDMC КАК НомерДокументаDMC,
	|	ТаблицаЗначений.ДатаДокументаDMC КАК ДатаДокументаDMC,
	|	ТаблицаЗначений.ПричинаОтсутствия КАК ПричинаОтсутствия,
	|	ТаблицаЗначений.ВремяНачалаДатой КАК ВремяНачалаДатой,
	|	ТаблицаЗначений.ВремяНачалаЧислом КАК ВремяНачалаЧислом,
	|	ТаблицаЗначений.ВремяОкончанияДатой КАК ВремяОкончанияДатой,
	|	ТаблицаЗначений.ВремяОкончанияЧислом КАК ВремяОкончанияЧислом,
	|	ТаблицаЗначений.СтрокаДокумента КАК СтрокаДокумента,
	|	ТаблицаЗначений.ДокументДвижения КАК ДокументДвижения
	|ИЗ
	|	ТаблицаЗначений КАК ТаблицаЗначений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Отсекающая КАК Отсекающая
	|		ПО ТаблицаЗначений.Сотрудник = Отсекающая.Сотрудник
	|			И ТаблицаЗначений.Дата = Отсекающая.Дата
	|			И ТаблицаЗначений.НомерДокументаDMC = Отсекающая.НомерДокументаDMC
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	Дата";

	
	
	Движения.УчетРабочегоВремениСотрудников.Загрузить(Запрос.Выполнить().Выгрузить());
	Движения.УчетРабочегоВремениСотрудников.Записывать = Истина;
	
КонецПроцедуры


Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат
	КонецЕсли;
	
	ЗаданияРасчета  = Перечисления.ЗаданияРасчета.ЗагрузкаДокументовDMC;
	КонсолидацияРегистрЗаданий.ЗаписатьПервичныеДанныеВРегистрЗаданий(Отказ,ЗаданияРасчета,Ссылка,Дата,Проведен,ПометкаУдаления);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

	Процедура ЗаполнитьТаблицуДвиженийБезПрогулов(Выборка,ТаблицаДвижений)

	Пока Выборка.Следующий() Цикл
		
		Для Счетчик = 0 По Выборка.РазностьДат Цикл
			
			НовоеДвижение = ТаблицаДвижений.Добавить();
			
			ЗаполнитьЗначенияСвойств(НовоеДвижение,Выборка);
			НовоеДвижение.Дата 			= Выборка.ДатаНачала + (86400 * Счетчик);
			
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти 

#КонецЕсли