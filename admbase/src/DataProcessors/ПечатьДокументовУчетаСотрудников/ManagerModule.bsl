////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//  
////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	СтруктураТипов = КонсолидацияОбщегоНазначения.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_КарточкаПроживающего") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПФ_MXL_КарточкаПроживающего",
			НСтр("ru = 'Карточка проживающего';
				|en = 'Resident card'"),
			СформироватьПечатнуюФормуКарточкаПроживающего(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуКарточкаПроживающего(СтруктураТипов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.АвтоМасштаб			= Истина;
	ТабДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабДокумент.ИмяПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ПФ_MXL_КарточкаПроживающего";
	
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
			
		Если ВРег(СтруктураОбъектов.Ключ) <> ВРег("Документ.ЗаселениеСотрудников")
			И ВРег(СтруктураОбъектов.Ключ) <> ВРег("Документ.ПеремещениеСотрудников")
			Тогда
			 
			ТекстСообщения = НСтр("ru = 'Формирование печатной формы ""Карточка проживающего"" не доступно для данного вида документов %1.';
									|en = 'You cannot generate print form Resident card for the %1 document kind.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, СтруктураОбъектов.Ключ);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			 
			Продолжить;
			
		КонецЕсли;
				
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
			
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СтруктураОбъектов.Ключ);
		ДанныеДляПечати = МенеджерОбъекта.ДанныеДляПечатнойФормыКарточкаПроживающего(ПараметрыПечати, СтруктураОбъектов.Значение);
		
		Если Не ДанныеДляПечати.Пустой() Тогда
			ЗаполнитьТабличныйДокументКарточкаПроживающего(ТабДокумент, ДанныеДляПечати, ОбъектыПечати);
		КонецЕсли;
	
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТабДокумент;
	
КонецФункции

Процедура ЗаполнитьТабличныйДокументКарточкаПроживающего(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьДокументовУчетаСотрудников.ПФ_MXL_КарточкаПроживающего");
	Макет.КодЯзыка = Метаданные.Языки.Русский.КодЯзыка;
	ПервыйДокумент = Истина;
	
	
	// Получаем области макета для вывода в табличный документ
	ОбластьКарточка   = Макет.ПолучитьОбласть("Карточка");
	
	Выборка = ДанныеДляПечати.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		СтруктураПараетров = Новый Структура;
		СтруктураПараетров.Вставить("ФИОСотрудника",			Выборка.ФИОСотрудника);
		СтруктураПараетров.Вставить("ДанныеПаспортаСотрудника",	Выборка.ДанныеПаспорта);
		СтруктураПараетров.Вставить("АдресРегистрации",			Выборка.АдресРегистрации);
		СтруктураПараетров.Вставить("СрокДействияРегистрации",	Формат(Выборка.СрокДействияРегистрации,"ДЛФ=ДД"));
		СтруктураПараетров.Вставить("СрокДействияПатента",		Формат(Выборка.СрокДействияПатента,"ДЛФ=ДД"));
		СтруктураПараетров.Вставить("МестоПроживания",			Выборка.МестоПроживания);
		СтруктураПараетров.Вставить("КомнатаНомер",				Выборка.КомнатаКод);
		СтруктураПараетров.Вставить("МестоНомер",				СтрШаблон("%1-%2",Выборка.КомнатаКод,
																						Выборка.Место));	
		СтруктураПараетров.Вставить("СтоимостьСутки",		 	Формат(Выборка.СтоимостьПроживания,"ЧЦ=10; ЧДЦ=0; ЧФ=""Ч рублей"""));
		СтруктураПараетров.Вставить("ДатаЗаселения",			Формат(Выборка.ДатаЗаселения,"ДЛФ=ДД"));	
		СтруктураПараетров.Вставить("ДатаВыселения",			Формат(Выборка.ДатаВыселения,"ДЛФ=ДД"));
		
		ОбластьКарточка.Параметры.Заполнить(СтруктураПараетров);
		
		ТабличныйДокумент.Вывести(ОбластьКарточка);
	
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
