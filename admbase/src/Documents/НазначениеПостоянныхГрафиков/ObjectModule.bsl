#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;

	ПроверкаКоррекностиВводаСотрудников(Отказ,РежимЗаписи);	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;
	
	МассивДаты =  Новый Массив;
	ВыбраннаяДата = НачалоМесяца(Дата);
	Пока ВыбраннаяДата < ТекущаяДатаСеанса() Цикл
	
		МассивДаты.Добавить(ВыбраннаяДата);
		
		ВыбраннаяДата = ДобавитьМесяц(ВыбраннаяДата,1);
	
	КонецЦикла;
	
	КонсолидацияРегистрЗаданий.ОтменитьРассчетВремениВПериоде(МассивДаты);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаИсходныеДанные.Сотрудник КАК Сотрудник,
	               |	ТаблицаИсходныеДанные.РабочееВремяНачало КАК РабочееВремяНачало,
	               |	ТаблицаИсходныеДанные.РабочееВремяОкончание КАК РабочееВремяОкончание,
	               |	ТаблицаИсходныеДанные.ПерерывНачало КАК ПерерывНачало,
	               |	ТаблицаИсходныеДанные.ПерерывОкончание КАК ПерерывОкончание,
	               |	НазначениеПостоянныхГрафиков.Дата КАК Период,
	               |	НазначениеПостоянныхГрафиков.СхемаРабочегоВремени КАК СхемаРабочегоВремени,
	               |	НазначениеПостоянныхГрафиков.Календарь КАК Календарь
	               |ИЗ
	               |	Документ.НазначениеПостоянныхГрафиков.ИсходныеДанные КАК ТаблицаИсходныеДанные
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НазначениеПостоянныхГрафиков КАК НазначениеПостоянныхГрафиков
	               |		ПО ТаблицаИсходныеДанные.Ссылка = НазначениеПостоянныхГрафиков.Ссылка
	               |ГДЕ
	               |	ТаблицаИсходныеДанные.Ссылка = &Ссылка
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Сотрудник,
	               |	Период";
	
	Движения.ГрафикиРаботПостоянные.Загрузить(Запрос.Выполнить().Выгрузить());
	Движения.ГрафикиРаботПостоянные.Записывать = Истина;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаКоррекностиВводаСотрудников(Отказ,РежимЗаписи)
	
	Если Не РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда 
		 Возврат
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсходныеДанные",ИсходныеДанные);
	Запрос.Текст = "ВЫБРАТЬ
	|	ИсходныеДанные.Сотрудник КАК Сотрудник,
	|	ИсходныеДанные.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	&ИсходныеДанные КАК ИсходныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанные.Сотрудник КАК Сотрудник,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИсходныеДанные.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ЗадвоенныеСтроки
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсходныеДанные.Сотрудник
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИсходныеДанные.НомерСтроки) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанные.Сотрудник КАК Сотрудник,
	|	ИсходныеДанные.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗадвоенныеСтроки КАК ЗадвоенныеСтроки
	|		ПО ИсходныеДанные.Сотрудник = ЗадвоенныеСтроки.Сотрудник
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	НомерСтроки";
	
	Результат = Запрос.Выполнить();
	Если  Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;

	Выборка = Результат.Выбрать();
	
	Массив = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Массив.Добавить(СтрШаблон("ном. строки %1; %2 %3",
						Строка(Выборка.НомерСтроки),
						Символы.Таб,
						Строка(Выборка.Сотрудник)))
	КонецЦикла;
	
	ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Задвоенные данные у сотрудников%1%2",Символы.ПС,СтрСоединить(Массив,Символы.ПС)),,,,Отказ);

КонецПроцедуры

#КонецОбласти 


#КонецЕсли