#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПодключитьсяКбазе(Ссылка) Экспорт  
	
	СтрокаСоединения = СтрокаСоединения(Ссылка);
	
	V83COMConnector= Новый COMОбъект("V83.COMConnector");
	Попытка
		Возврат V83COMConnector.Connect(СтрокаСоединения);
	Исключение
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Невозможно установить соединение: %1%2",Символы.ПС,ОписаниеОшибки()));
		Возврат Неопределено;
	КонецПопытки;

КонецФункции

Функция ПолучитьСоединениеHTTP(Ссылка,ИмяПубликации)  Экспорт 
	
	Если Не ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Истина)
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УчетныеДанныеПодключений.АдресСервера КАК АдресСервера,
	|	УчетныеДанныеПодключений.ИмяПубликации КАК ИмяПубликации,
	|	УчетныеДанныеПодключений.КорневойURL КАК КорневойURL,
	|	УчетныеДанныеПодключений.ИмяПользователяСервиса КАК ИмяПользователя,
	|	УчетныеДанныеПодключений.ПарольСервиса КАК Пароль
	|ИЗ
	|	Справочник.УчетныеДанныеПодключений КАК УчетныеДанныеПодключений
	|ГДЕ
	|	УчетныеДанныеПодключений.Ссылка = &Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ИмяПубликации = Выборка.ИмяПубликации;
	Возврат Новый HTTPСоединение(Выборка.АдресСервера, , Выборка.ИмяПользователя, Выборка.Пароль);

КонецФункции // ПолучитьСоединениеHTTP()

Функция ПолучитьПроксиСоедиение(Ссылка) Экспорт 
	
	Если Не ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Истина)
	КонецЕсли; 

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УчетныеДанныеПодключений.WebСервисИмяБазы КАК ИмяБазы,
	|	УчетныеДанныеПодключений.WebСервисИмяСервера КАК ИмяСервера,
	|	УчетныеДанныеПодключений.WebСервисИмяПользователя КАК ИмяПользователя,
	|	УчетныеДанныеПодключений.WebСервисПароль КАК Пароль
	|ИЗ
	|	Справочник.УчетныеДанныеПодключений КАК УчетныеДанныеПодключений
	|ГДЕ
	|	УчетныеДанныеПодключений.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();

	WSОпределения 	= Новый WSОпределения(СтрШаблон("http://%1/%2/ws/deviation/?wsdl",Выборка.ИмяСервера,Выборка.ИмяБазы),
											Выборка.ИмяПользователя,
											Выборка.Пароль);
	WSПрокси 		= Новый WSПрокси(WSОпределения, 
							"https://whitehills.ru/deviation",
							"deviation", 
							"deviationSoap");
	
	WSПрокси.Пользователь 	= Выборка.ИмяПользователя;
	WSПрокси.Пароль 		= Выборка.Пароль;

	Возврат WSПрокси;
	
КонецФункции // ПолучитьПроксиСоедиение()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтрокаСоединения(Ссылка)
	
	Если Не ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Истина)
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УчетныеДанныеПодключений.ИмяБазы КАК ИмяБазы,
	|	УчетныеДанныеПодключений.ИмяСервера КАК ИмяСервера,
	|	УчетныеДанныеПодключений.ИмяПользователя КАК ИмяПользователя,
	|	УчетныеДанныеПодключений.Пароль КАК Пароль
	|ИЗ
	|	Справочник.УчетныеДанныеПодключений КАК УчетныеДанныеПодключений
	|ГДЕ
	|	УчетныеДанныеПодключений.Ссылка = &Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	
	Возврат СтрШаблон("Srvr=""%1"";Ref=""%2"";Usr=""%3"";Pwd=""%4"";",
											Выборка.ИмяСервера, 
											Выборка.ИмяБазы,
											Выборка.ИмяПользователя,
											Выборка.Пароль);
											
КонецФункции

#КонецОбласти

#КонецЕсли