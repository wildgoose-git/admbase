
////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//  
////////////////////////////////////////////////////////////////////////////////
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаселениеСотрудников") Тогда
		
		ЗаполнитьПоЗаселениюСотрудников(ДанныеЗаполнения);
		
		Отвественный = Пользователи.ТекущийПользователь();
		ДатаЗаселения = ТекущаяДатаСеанса();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ПроверкаЗаселенных(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
#Область ТекстЗапроса

	Запрос.Текст = "ВЫБРАТЬ
	|	ЗанятыеМестаОбщежитие.Регистратор,
	|	ЗанятыеМестаОбщежитие.АдресРегистрации,
	|	ЗанятыеМестаОбщежитие.Комната,
	|	ЗанятыеМестаОбщежитие.Место
	|ИЗ
	|	РегистрСведений.ЗанятыеМестаОбщежитие КАК ЗанятыеМестаОбщежитие
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеСотрудников.ДетальныеЗаписи КАК ПеремещениеСотрудниковДетальныеЗаписи
	|		ПО ПеремещениеСотрудниковДетальныеЗаписи.Сотрудник = ЗанятыеМестаОбщежитие.Сотрудник
	|ГДЕ
	|	ПеремещениеСотрудниковДетальныеЗаписи.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеремещениеСотрудников.Дата КАК Период,
	|	ПеремещениеСотрудников.Дата КАК ДатаЗаписи,
	|	ПеремещениеСотрудников.Ссылка КАК Регистратор,
	|	ПеремещениеСотрудников.АдресРегистрации,
	|	ПеремещениеСотрудников.ДатаЗаселения,
	|	ПеремещениеСотрудниковДетальныеЗаписи.Сотрудник,
	|	ПеремещениеСотрудниковДетальныеЗаписи.Комната,
	|	ПеремещениеСотрудниковДетальныеЗаписи.Место,
	|	ПеремещениеСотрудников.СтоимостьПроживания
	|ИЗ
	|	Документ.ПеремещениеСотрудников.ДетальныеЗаписи КАК ПеремещениеСотрудниковДетальныеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеСотрудников КАК ПеремещениеСотрудников
	|		ПО ПеремещениеСотрудниковДетальныеЗаписи.Ссылка = ПеремещениеСотрудников.Ссылка
	|ГДЕ
	|	ПеремещениеСотрудниковДетальныеЗаписи.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПеремещениеСотрудниковДетальныеЗаписи.НомерСтроки";

#КонецОбласти

	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.ЗанятыеМестаОбщежитие.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		НаборЗаписей.Отбор.АдресРегистрации.Установить(Выборка.АдресРегистрации);
		НаборЗаписей.Отбор.Комната.Установить(Выборка.Комната);
		НаборЗаписей.Отбор.Место.Установить(Выборка.Место);
		НаборЗаписей.ОбменДанными.Загрузка =Истина;
		НаборЗаписей.Записать();
	КонецЦикла;
	
					   
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Движения.ИсторияРазмещенияСотрудников.Добавить(),Выборка);
		ЗаполнитьЗначенияСвойств(Движения.ЗанятыеМестаОбщежитие.Добавить(),Выборка);
	КонецЦикла;
	
	Движения.ИсторияРазмещенияСотрудников.Записывать = Истина;
	Движения.ЗанятыеМестаОбщежитие.Записывать = Истина;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДетальныеЗаписи.Очистить();	

КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаЗаселенных(Отказ)

Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДетальныеЗаписи", ДетальныеЗаписи.Выгрузить());

#Область ТекстЗапроса

	Запрос.Текст = "ВЫБРАТЬ
	|	ДетальныеЗаписи.Сотрудник,
	|	ДетальныеЗаписи.НомерСтроки
	|ПОМЕСТИТЬ ДетальныеЗаписи
	|ИЗ
	|	&ДетальныеЗаписи КАК ДетальныеЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Представление(ЗанятыеМестаОбщежитие.Сотрудник) КАК Сотрудник,
	|	ДетальныеЗаписи.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ДетальныеЗаписи КАК ДетальныеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗанятыеМестаОбщежитие КАК ЗанятыеМестаОбщежитие
	|		ПО ДетальныеЗаписи.Сотрудник = ЗанятыеМестаОбщежитие.Сотрудник
	|ГДЕ
	|	ЗанятыеМестаОбщежитие.Сотрудник ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

#КонецОбласти

	Результат =Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;

	Массив = Новый Массив;
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл

		Массив.Добавить(СтрШаблон("стр.%1 %2 не найдено размещение", Выборка.НомерСтроки, 
														Выборка.Сотрудник));

	КонецЦикла;

	ОбщегоНазначения.СообщитьПользователю(СтрСоединить(Массив, Символы.ПС), , , , Отказ);

КонецПроцедуры

Процедура ЗаполнитьПоЗаселениюСотрудников(ДанныеЗаполнения)
	
	Если Не ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);

#Область ТекстЗапроса

	Запрос.Текст = "ВЫБРАТЬ
	|	ПеремещениеСотрудниковДетальныеЗаписи.НомерСтроки КАК НомерСтроки,
	|	ПеремещениеСотрудниковДетальныеЗаписи.Сотрудник
	|ИЗ
	|	Документ.ПеремещениеСотрудников.ДетальныеЗаписи КАК ПеремещениеСотрудниковДетальныеЗаписи
	|ГДЕ
	|	ПеремещениеСотрудниковДетальныеЗаписи.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

#КонецОбласти

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДетальныеЗаписи.Добавить(), Выборка);
	КонецЦикла;

КонецПроцедуры


#КонецОбласти 

#КонецЕсли


