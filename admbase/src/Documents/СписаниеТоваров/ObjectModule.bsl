
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
#Область ТекстЗапроса

Запрос.Текст = "ВЫБРАТЬ
|	СписаниеТоваров.Дата КАК Период,
|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
|	СписаниеТоваров.МестоХранения КАК МестоХранения,
|	СписаниеТоваровТовары.Номенклатура КАК Номенклатура,
|	СписаниеТоваровТовары.Количество КАК Количество
|ИЗ
|	Документ.СписаниеТоваров.Товары КАК СписаниеТоваровТовары
|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеТоваров КАК СписаниеТоваров
|		ПО СписаниеТоваровТовары.Ссылка = СписаниеТоваров.Ссылка
|ГДЕ
|	СписаниеТоваровТовары.Ссылка = &Ссылка
|
|УПОРЯДОЧИТЬ ПО
|	СписаниеТоваровТовары.НомерСтроки";
	
#КонецОбласти

	
	Движения.ОстаткиПродуктовНаСкладах.Записывать = Истина;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Движения.ОстаткиПродуктовНаСкладах.Добавить(),Выборка)
	КонецЦикла;

	
		
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИнвентаризацияТоваров") Тогда
		
		ЗаполнитьПоИнветаризации(ДанныеЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоИнветаризации(ДанныеЗаполнения)
	
	Если Не ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Истина)
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",ДанныеЗаполнения);
	Запрос.Текст = "ВЫБРАТЬ
	|	ИнвентаризацияТоваровТовары.Номенклатура КАК Номенклатура,
	|	ИнвентаризацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ИнвентаризацияТоваровТовары.Количество - ИнвентаризацияТоваровТовары.КоличествоФакт КАК Количество,
	|	ИнвентаризацияТоваровТовары.Вес - ИнвентаризацияТоваровТовары.ВесФакт КАК Вес
	|ИЗ
	|	Документ.ИнвентаризацияТоваров.Товары КАК ИнвентаризацияТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияТоваров КАК ИнвентаризацияТоваров
	|		ПО ИнвентаризацияТоваровТовары.Ссылка = ИнвентаризацияТоваров.Ссылка
	|ГДЕ
	|	ИнвентаризацияТоваровТовары.Ссылка = &Ссылка
	|	И ИнвентаризацияТоваровТовары.Количество - ИнвентаризацияТоваровТовары.КоличествоФакт > 0
	|	И ИнвентаризацияТоваров.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияТоваров.МестоХранения КАК МестоХранения,
	|	ИнвентаризацияТоваров.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.ИнвентаризацияТоваров КАК ИнвентаризацияТоваров
	|ГДЕ
	|	ИнвентаризацияТоваров.Ссылка = &Ссылка
	|	И ИнвентаризацияТоваров.Проведен";
	
	Результат = Запрос.ВыполнитьПакет();
	
	Товары.Загрузить(Результат[0].Выгрузить());

	Выборка = Результат[1].Выбрать();
	Выборка.Следующий();
	
	МестоХранения 		= Выборка.МестоХранения;
	ДокументОснование 	= Выборка.ДокументОснование;
	
	
КонецПроцедуры

#КонецОбласти 

//СДЕЛАТЬ КОНТРОЛЬ ПРОВЕДЕНИЯ И ОТМЕНЫ ПРОВЕДЕНИЯ
#КонецЕсли